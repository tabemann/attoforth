FORTH-WORDLIST IO-WORDLIST TASK-WORDLIST 3 SET-ORDER

IO-WORDLIST SET-CURRENT

2 CELLS CONSTANT TERMINAL-INFO-SIZE
0 CELLS OFFSET TERMINAL-FD
1 CELLS OFFSET TERMINAL-BUFFER-SIZE

: EXPAND-TERMINAL-BUFFER ( source -- )
  DUP INPUT-ARG @ TERMINAL-BUFFER-SIZE @ DUP 2 * DUP ALLOCATE!
  SWAP 3 PICK INPUT-ARG @ TERMINAL-BUFFER-SIZE !
  2 PICK INPUT-BUFFER @ DUP 2 PICK 4 ROLL MOVE FREE!
  SWAP INPUT-BUFFER ! ;

: TRY-EXPAND-TERMINAL-BUFFER ( source -- )
  DUP INPUT-COUNT @ OVER INPUT-ARG @ TERMINAL-BUFFER-SIZE @ = IF
    EXPAND-TERMINAL-BUFFER
  ELSE DROP THEN ;

: READ-TERMINAL-CHAR ( source -- continue )
  HERE DUP 1 3 PICK INPUT-ARG @ TERMINAL-FD @ IO-READ-BLOCK
  DUP IO-ACTION-GET-STATE DUP IO-STATE-GET-INDEX 1 = SWAP
  IO-STATE-DESTROY SWAP IO-ACTION-DESTROY IF
    C@ DUP NEWLINE = IF
      SPACE OVER TRY-EXPAND-TERMINAL-BUFFER
      OVER INPUT-BUFFER @ ROT INPUT-COUNT DUP @ 1+ DUP ROT ! + 1- C!
      FALSE
    ELSE DUP 127 = IF
      DROP 8 EMIT SPACE 8 EMIT DUP INPUT-COUNT @ 0> IF
        DUP INPUT-COUNT @ 1 - SWAP INPUT-COUNT !
      ELSE DROP THEN TRUE
    ELSE
      DUP EMIT OVER TRY-EXPAND-TERMINAL-BUFFER
      OVER INPUT-BUFFER @ ROT INPUT-COUNT DUP @ 1+ DUP ROT ! + 1- C! TRUE
    THEN THEN
  ELSE DROP TRUE SWAP INPUT-IS-CLOSED ! FALSE THEN ;

: REFILL-TERMINAL ( source -- flag )
  0 OVER INPUT-COUNT ! 0 OVER INPUT-INDEX !
  BEGIN DUP READ-TERMINAL-CHAR INVERT UNTIL DROP TRUE ;

256 CONSTANT DEFAULT-TERMINAL-BUFFER-SIZE

: TERMINAL>SOURCE-CLEANUP ( source -- )
  DUP INPUT-BUFFER @ FREE! DUP INPUT-ARG @ FREE! FREE! ;

: TERMINAL>SOURCE ( fd -- input )
  INPUT-SIZE ALLOCATE! TERMINAL-INFO-SIZE ALLOCATE!
  DEFAULT-TERMINAL-BUFFER-SIZE OVER TERMINAL-BUFFER-SIZE !
  ROT OVER TERMINAL-FD ! OVER INPUT-ARG !
  DEFAULT-TERMINAL-BUFFER-SIZE ALLOCATE! OVER INPUT-BUFFER !
  0 OVER INPUT-INDEX ! 0 OVER INPUT-COUNT !
  FALSE OVER INPUT-IS-CLOSED !
  ['] TERMINAL>SOURCE-CLEANUP OVER INPUT-CLEANUP !
  ['] REFILL-TERMINAL OVER INPUT-REFILL !
  0 OVER INPUT-SOURCE-ID ! 0 OVER INPUT-NEXT-INPUT ! ;


:NONAME
  IO-STDIN TERMINAL>SOURCE THIS-TASK CLEANUP-INPUT THIS-TASK >CONSOLE-INPUT
  FORTH-WORDLIST DUP 1 SET-ORDER SET-CURRENT ABORT ; EXECUTE