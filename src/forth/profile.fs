\ Copyright (c) 2018, Travis Bemann
\ All rights reserved.
\ 
\ Redistribution and use in source and binary forms, with or without
\ modification, are permitted provided that the following conditions are met:
\ 
\ 1. Redistributions of source code must retain the above copyright notice,
\    this list of conditions and the following disclaimer.
\ 
\ 2. Redistributions in binary form must reproduce the above copyright notice,
\    this list of conditions and the following disclaimer in the documentation
\    and/or other materials provided with the distribution.
\ 
\ 3. Neither the name of the copyright holder nor the names of its
\    contributors may be used to endorse or promote products derived from
\    this software without specific prior written permission.
\ 
\ THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
\ AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
\ IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
\ ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
\ LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
\ CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
\ SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
\ INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
\ CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
\ ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
\ POSSIBILITY OF SUCH DAMAGE.

GET-ORDER GET-CURRENT BASE @

DECIMAL
FORTH-WORDLIST TASK-WORDLIST 2 SET-ORDER
FORTH-WORDLIST SET-CURRENT

NEW-LOCK CONSTANT PROFILE-LOCK
VARIABLE LAST-PROFILE-WORD 0 LAST-PROFILE-WORD !
VARIABLE PROFILE-INSTRUMENT-ENABLE FALSE PROFILE-INSTRUMENT-ENABLE !
VARIABLE PROFILE-ENABLE FALSE PROFILE-ENABLE !

: UPDATE-LAST-PROFILE-WORD ( addr -- addr )
  PROFILE-LOCK LOCK LAST-PROFILE-WORD @ SWAP LAST-PROFILE-WORD !
  PROFILE-LOCK UNLOCK ;

BEGIN-STRUCTURE PROFILE-DATA-SIZE
  FIELD: PROFILE-XT
  FIELD: PROFILE-NEXT
  FIELD: PROFILE-COUNT
END-STRUCTURE

: SET-PROFILE-INSTRUMENT-ENABLE ( f -- ) PROFILE-INSTRUMENT-ENABLE ! ;

: GET-PROFILE-INSTRUMENT-ENABLE ( -- f ) PROFILE-INSTRUMENT-ENABLE @ ;

: SET-PROFILE-ENABLE ( f -- ) PROFILE-ENABLE ! ;

: GET-PROFILE-ENABLE ( -- f ) PROFILE-ENABLE @ ;

: PROFILE. ( -- )
  LAST-PROFILE-WORD @
  BEGIN DUP 0<> WHILE
    CR DUP PROFILE-XT @ DUP NAME>STRING TYPE SPACE 16 BASE.
    DUP PROFILE-COUNT @ 10 BASE.
    PROFILE-NEXT @
  REPEAT
  DROP ;

: RESET-PROFILE ( -- )
  LAST-PROFILE-WORD @
  BEGIN DUP 0<> WHILE
    DUP 0 SWAP PROFILE-COUNT !
    PROFILE-NEXT @
  REPEAT
  DROP ;

: COMPILE-PROFILE ( -- )
  GET-PROFILE-INSTRUMENT-ENABLE IF
    & BRANCH HERE [ PROFILE-DATA-SIZE 2 CELLS + ] LITERAL + ,
    0 , HERE UPDATE-LAST-PROFILE-WORD LATESTXT , , HERE 0 ,
    & GET-PROFILE-ENABLE & IF
      & (LITERAL) 1 , SWAP & (LITERAL) , & +!
    & THEN
  THEN ;

: :NONAME ( -- xt ) :NONAME COMPILE-PROFILE ;

: : ( "name" -- ) : COMPILE-PROFILE ;

BASE ! SET-CURRENT SET-ORDER